# Facets Module Validation Rules

This document contains validation rules for Facets modules. Each rule includes bad and good examples.

---

## Sample.spec Rules

### RULE-001: Required fields must be present

All required fields from spec schema must exist in sample, even with empty values.

**Bad:**
```yaml
sample:
  spec: {}
```

**Good:**
```yaml
sample:
  spec:
    cloud_account: ""
    region: ""
```

---

### RULE-002: Enum values must match schema

Sample values must be valid enum options defined in the spec schema.

**Bad:**
```yaml
sample:
  spec:
    cluster_addons:
      addon1:
        name: metrics-server  # Not in allowed enum
```

**Good:**
```yaml
sample:
  spec:
    cluster_addons:
      addon1:
        name: eks-node-agent  # Valid enum value
```

---

### RULE-003: Use {} for objects, [] for arrays, never null

When schema defines `type: object`, use `{}`. When schema defines `type: array`, use `[]`. Never use `null`.

**Bad:**
```yaml
sample:
  spec:
    values: null
    tolerations: []  # Wrong if schema says type: object
```

**Good:**
```yaml
sample:
  spec:
    values: {}
    tolerations: {}  # Correct for type: object with patternProperties
```

---

## var.inputs Rules

To build correct `var.inputs` types, verify the schema of each input type in `outputs/{type-name}/outputs.yaml` or via `raptor get output-type <type>`.

### RULE-004: Explicit object type required

`var.inputs` must use explicit `object({...})`, NOT `type = any` or `map(any)`.

**Bad:**
```hcl
variable "inputs" {
  type = any
}

variable "inputs" {
  type = map(any)
}
```

**Good:**
```hcl
variable "inputs" {
  type = object({
    cloud_account = object({
      attributes = optional(object({
        region = optional(string)
      }), {})
      interfaces = optional(object({}), {})
    })
  })
}
```

---

### RULE-005: All facets.yaml inputs must exist in var.inputs

Every input declared in facets.yaml must have a corresponding entry in var.inputs.

**facets.yaml:**
```yaml
inputs:
  cloud_account:
    type: "@facets/aws_cloud_account"
  kubernetes_details:
    type: "@facets/eks"
```

**Bad (missing kubernetes_details):**
```hcl
variable "inputs" {
  type = object({
    cloud_account = object({...})
    # kubernetes_details is missing!
  })
}
```

**Good:**
```hcl
variable "inputs" {
  type = object({
    cloud_account = object({...})
    kubernetes_details = object({
      attributes = optional(object({...}), {})
      interfaces = optional(object({}), {})
    })
  })
}
```

---

### RULE-006: var.inputs structure depends on the source module's output key

The structure of `var.inputs.<name>` — declaration in `variables.tf` and access in `main.tf` — is determined by **which named output key** the source module uses. Check the source module's `facets.yaml` `outputs:` section to find the key name, then apply:

| Source output key | variables.tf | main.tf access |
|---|---|---|
| `default` | `attributes = optional(object({...}), {})` + `interfaces = optional(object({}), {})` | `var.inputs.X.attributes.field` |
| `attributes` | flat fields directly | `var.inputs.X.field` |

Do not rely on the output type schema alone — two types can have identical schemas but inject differently based on the key name. In blueprints, when consuming a non-default output key, specify `"output_name"` in the input wiring (e.g., `"output_name": "attributes"`).

**Bad — `default` output declared flat (missing wrapper):**
```hcl
aks_cluster = object({
  oidc_issuer_url = optional(string)  # Wrong: must use attributes/interfaces wrapper
  cluster_id      = optional(string)
})
```

**Bad — `attributes` output declared with wrapper (wrapper not needed):**
```hcl
notification_channels = object({
  attributes = optional(object({          # Wrong: source uses outputs.attributes, must be flat
    channel_names = optional(map(string), {})
  }), {})
  interfaces = optional(object({}), {})
})
```

**Good — `default` output (wrapper + correct access):**
```hcl
aks_cluster = object({
  attributes = optional(object({
    oidc_issuer_url = optional(string)
    cluster_id      = optional(string)
  }), {})
  interfaces = optional(object({}), {})
})
# access: var.inputs.aks_cluster.attributes.oidc_issuer_url
```

**Good — `attributes` output (flat + correct access):**
```hcl
notification_channels = object({
  channel_ids   = optional(map(string), {})
  channel_names = optional(map(string), {})
  project_id    = optional(string)
})
# access: var.inputs.notification_channels.channel_names
```

---

## Spec Schema Rules

### RULE-007: No regex lookahead/lookbehind

JSON Schema regex does not support `(?=)`, `(?!)`, `(?<=)`, `(?<!)`.

**Bad:**
```yaml
properties:
  port:
    type: string
    pattern: ^(?!0$)([1-9][0-9]{0,3}|[1-5][0-9]{4})$
```

**Good:**
```yaml
properties:
  port:
    type: string
    pattern: ^([1-9][0-9]{0,3}|[1-5][0-9]{4})$
```

**Bad (domain validation):**
```yaml
pattern: ^(?=.{1,253}$)(?!-)[A-Za-z0-9-]{1,63}(?<!-)(\..*)?$
```

**Good:**
```yaml
pattern: ^[A-Za-z0-9]([A-Za-z0-9-]{0,61}[A-Za-z0-9])?(\.[A-Za-z0-9]([A-Za-z0-9-]{0,61}[A-Za-z0-9])?)*$
```

---

### RULE-008: No duplicate enum values

Enum arrays must not contain duplicate values.

**Bad:**
```yaml
properties:
  header_name:
    enum:
      - X-Frame-Options
      - Cache-Control
      - Cache-Control  # Duplicate!
      - Vary
```

**Good:**
```yaml
properties:
  header_name:
    enum:
      - X-Frame-Options
      - Cache-Control
      - Vary
```

---

### RULE-009: required inside patternProperties

Place `required` array inside the pattern definition, not as a sibling of `patternProperties`.

**Bad:**
```yaml
rules:
  type: object
  patternProperties:
    ^[a-zA-Z0-9_.-]*$:
      type: object
      properties:
        service_name: {type: string}
        path: {type: string}
        port: {type: string}
  required:  # Wrong! This is sibling of patternProperties
    - service_name
    - path
    - port
```

**Good:**
```yaml
rules:
  type: object
  patternProperties:
    ^[a-zA-Z0-9_.-]*$:
      type: object
      properties:
        service_name: {type: string}
        path: {type: string}
        port: {type: string}
      required:  # Correct! Inside the pattern definition
        - service_name
        - path
        - port
```

---

## Output Schema Rules

### RULE-010: Explicit type: object for nested objects

Every nested object in output schema must have explicit `type: object` field.

**Bad:**
```yaml
# outputs/my-type/outputs.yaml
attributes:
  properties:
    region:
      type: string
```

**Good:**
```yaml
# outputs/my-type/outputs.yaml
attributes:
  type: object
  properties:
    region:
      type: string
```

---

### RULE-011: No union types

Don't use array syntax for types in output schemas.

**Bad:**
```yaml
properties:
  vpc_endpoint_id:
    type:
      - string
      - "null"
```

**Good:**
```yaml
properties:
  vpc_endpoint_id:
    type: string
```

---

### RULE-012: Field names must match actual outputs

Output schema field names must match what the module actually outputs in `locals.tf`.

**Bad (schema says `project` but module outputs `project_id`):**
```yaml
# outputs.yaml
attributes:
  type: object
  properties:
    project:
      type: string
```

```hcl
# locals.tf
output_attributes = {
  project_id = data.google_project.current.project_id
}
```

**Good:**
```yaml
# outputs.yaml
attributes:
  type: object
  properties:
    project_id:
      type: string
```

```hcl
# locals.tf
output_attributes = {
  project_id = data.google_project.current.project_id
}
```

---

## Terraform Rules

### RULE-013: No required_providers in modules

Modules should not define `required_providers` blocks. Providers are injected by the Facets platform.

**Bad:**
```hcl
terraform {
  required_version = ">= 1.0"

  required_providers {
    kubernetes = {
      source  = "hashicorp/kubernetes"
      version = "~> 2.23"
    }
  }
}
```

**Good:**
```hcl
terraform {
  required_version = ">= 1.0"
}
```

**How provider injection works — module-scoped, not output-scoped:**

Providers are determined by the *source module*, not by which specific output type the consuming module declares as its input type. If a source module exposes providers on *any* of its outputs (e.g., `@facets/kubernetes-details` on the `attributes` output), those providers are available to any consuming module that wires to *any* output of that source module (e.g., `@facets/eks` on the `default` output).

This means: you do not need to pick a specific output type just to get provider injection. Pick the output type that best matches the **data contract** you need; provider availability follows automatically from the source module.

---

### RULE-014: All referenced variables must be declared

Every variable referenced in Terraform code must be declared in `variables.tf`. Platform-injected variables like `var.cc_metadata`, `var.cluster`, `var.baseinfra` do not exist in modules — use `var.instance` and `var.inputs` instead.

**Bad:**
```hcl
# main.tf
resource "kubernetes_namespace" "ns" {
  metadata {
    name = var.cc_metadata.namespace  # cc_metadata not declared!
  }
}
```

**Good:**
```hcl
# variables.tf
variable "instance" {
  type = object({
    spec = object({
      namespace = optional(string)
    })
  })
}

# main.tf
resource "kubernetes_namespace" "ns" {
  metadata {
    name = var.instance.spec.namespace
  }
}
```

---

### RULE-015: Use lookup() for optional spec fields

**Source:** #211, #206, #238
**Category:** Terraform

Accessing optional spec fields directly causes Terraform errors when the field is not provided. Always use `lookup()` with a sensible default.

**Bad:**
```hcl
# Fails when metadata is not present in spec
namespace = var.instance.metadata.namespace

# Fails when interruption_handling is not in spec
enable_interruption = var.instance.spec.interruption_handling
```

**Good:**
```hcl
namespace = lookup(lookup(var.instance, "metadata", {}), "namespace", "default")

enable_interruption = lookup(var.instance.spec, "interruption_handling", false)
```

---

### RULE-017: depends_on — when to use and when not to

**Source:** #210, #200
**Category:** Terraform

Use `depends_on` only when no attribute reference creates an implicit dependency. Adding `depends_on` to a resource that already references another resource's attributes is redundant and can cause Terraform cycles.

**Case A — Don't add depends_on when attribute references exist:**
```hcl
# Bad
resource "aws_eks_addon" "ebs_csi" {
  cluster_name = aws_eks_cluster.main.name  # Already implicit dependency
  depends_on   = [aws_eks_cluster.main]      # Unnecessary and can cause cycles
}

# Good
resource "aws_eks_addon" "ebs_csi" {
  cluster_name = aws_eks_cluster.main.name  # Terraform infers dependency automatically
}
```

**Case B — depends_on IS required for CRD resources (no attribute reference exists):**
```hcl
# Bad
resource "kubectl_manifest" "karpenter_nodepool" {
  yaml_body = yamlencode({
    apiVersion = "karpenter.sh/v1"
    kind       = "NodePool"
  })
  # Missing depends_on - CRD may not exist yet!
}

# Good
resource "kubectl_manifest" "karpenter_nodepool" {
  yaml_body = yamlencode({
    apiVersion = "karpenter.sh/v1"
    kind       = "NodePool"
  })
  depends_on = [helm_release.karpenter]  # Ensure CRDs are installed first
}
```

---

### RULE-018: Pin Docker images to verified tags

**Source:** #204, #201
**Category:** Terraform

Using non-existent image tags causes `ImagePullBackOff` errors. Using `:latest` breaks reproducibility. Always use verified, pinned image tags from official registries.

**Bad:**
```hcl
# Tag doesn't exist on this registry
image = "bitnami/kubectl:1.31.4"

# Latest tag breaks reproducibility
image = "kubectl:latest"
```

**Good:**
```hcl
# Verified official image with correct tag format
image = "registry.k8s.io/kubectl:v1.31.4"
```

---

### RULE-019: Single owner for shared resource tags

**Source:** #234
**Category:** Terraform

When multiple modules manage the same tag on a shared resource (e.g., subnets), Terraform oscillates between states on every apply. Only the resource-owning module should manage its tags; other modules should use data sources to read.

**Bad (two modules setting the same tag on a shared subnet):**
```hcl
# In network module
resource "aws_subnet" "private" {
  tags = {
    "karpenter.sh/discovery" = var.cluster_name
  }
}

# In karpenter module - CONFLICT!
resource "aws_ec2_tag" "subnet_discovery" {
  resource_id = data.aws_subnet.private.id
  key         = "karpenter.sh/discovery"
  value       = var.cluster_name
}
```

**Good (consuming module reads via data source, does not manage tags):**
```hcl
data "aws_subnets" "private" {
  filter {
    name   = "tag:karpenter.sh/discovery"
    values = [var.cluster_name]
  }
}
```

---

## facets.yaml Rules

### RULE-020: No unsupported metadata in facets.yaml

**Source:** #212
**Category:** facets.yaml

`metadata:` is not a supported top-level key in facets.yaml. Do not define metadata schemas or include metadata in sample blocks. Use `var.environment.namespace` for namespace access instead.

**Bad:**
```yaml
# Root-level metadata schema - NOT SUPPORTED
metadata:
  type: object
  properties:
    namespace:
      type: string

sample:
  kind: service
  flavor: aws
  metadata:          # NOT SUPPORTED in sample
    namespace: test
  spec:
    replicas: 1
```

**Good:**
```yaml
# No metadata at root level or in sample
sample:
  kind: service
  flavor: aws
  spec:
    replicas: 1

# In Terraform, use var.environment for namespace:
# namespace = var.environment.namespace
```

---

### RULE-021: facets.yaml must include intentDetails

**Source:** #153, #185
**Category:** facets.yaml

Every facets.yaml must include an `intentDetails` block. Missing `intentDetails` causes UI rendering issues and validation warnings.

**Bad:**
```yaml
intent: service
flavor: aws
version: "1.0"
description: Deploy a service on AWS
# Missing intentDetails!
```

**Good:**
```yaml
intent: service
flavor: aws
version: "1.0"
description: Deploy a service on AWS
intentDetails:
  type: Cloud & Infrastructure
  description: Deploy and manage containerized applications
  displayName: Service
  iconUrl: https://raw.githubusercontent.com/Facets-cloud/facets-modules/master/icons/service.svg
```

**Valid `intentDetails.type` values:**
- `Cloud & Infrastructure`
- `Datastores`
- `Kubernetes`
- `Monitoring & Observability`
- `Operators`

---

## Module Design & Lifecycle Rules

### RULE-022: Enable security defaults

**Source:** #218
**Category:** Module design

Security features like encryption, logging, and monitoring should be enabled by default. Users can opt out explicitly, but the default should be secure.

**Bad:**
```hcl
# Encryption disabled by default - insecure
enable_encryption = lookup(var.instance.spec, "enable_encryption", false)

# No logging by default
enable_logging = lookup(var.instance.spec, "enable_logging", false)
```

**Good:**
```hcl
# Encryption enabled by default - secure
enable_encryption = lookup(var.instance.spec, "enable_encryption", true)

# Logging enabled by default
enable_logging = lookup(var.instance.spec, "enable_logging", true)
```

---

### RULE-023: Bump version for breaking changes and update project types

**Category:** Module lifecycle

When introducing a breaking change to a module (e.g., changing input/output types, renaming spec fields, removing features), increment the version by `0.1` in `facets.yaml`. The directory name stays the same — only the `version:` field in facets.yaml changes. If the module is referenced in any base project type (`project-type/{cloud}/project-type.yml`), update the version there too.

**Bad (breaking change without version bump):**
```yaml
# facets.yaml - changed output type but kept same version
intent: kubernetes_cluster
flavor: eks_standard
version: "1.0"          # Still 1.0 despite breaking output change!
outputs:
  default:
    type: "@facets/eks-v2"   # Was @facets/eks — breaking change
```

**Good (version bumped, project type updated):**
```yaml
# facets.yaml
intent: kubernetes_cluster
flavor: eks_standard
version: "1.1"          # Bumped from 1.0 to 1.1
outputs:
  default:
    type: "@facets/eks-v2"
```

```yaml
# project-type/aws/project-type.yml — updated to use new version
- intent: kubernetes_cluster
  flavor: eks_standard
  version: "1.1"        # Was "1.0", updated to match
```

**What counts as a breaking change:**
- Changing an output type (consumers may break)
- Removing or renaming a spec field
- Changing input types or removing inputs
- Altering output attribute/interface structure

**What does NOT require a version bump:**
- Adding new optional spec fields
- Bug fixes that don't change the contract
- Adding new outputs (existing consumers unaffected)

---

### RULE-024: Use //name module for resource name length limits

**Category:** Terraform

All modules should use the `//name` utility module to generate resource names that respect cloud provider length limits. Hardcoding names or simple concatenation can exceed provider limits and cause deployment failures.

**Bad:**
```hcl
resource "aws_eks_cluster" "main" {
  name = "${var.environment.name}-${var.instance_name}-cluster"  # May exceed 63-char limit
}
```

**Good:**
```hcl
module "name" {
  source        = "github.com/Facets-cloud/facets-utility-modules//name"
  environment   = var.environment
  limit         = 63
  resource_name = var.instance_name
  resource_type = "kubernetes_cluster"
}

resource "aws_eks_cluster" "main" {
  name = module.name.name
}
```

---

### RULE-025: Complete schema for var.instance and All variables

**Category:** Terraform

All variable declarations must use explicit type schemas. Never use `type = any` for any variable, especially `var.instance`. The `var.instance` variable must fully define the module's configuration schema with all nested objects and fields explicitly typed.

**Bad:**
```hcl
variable "instance" {
  description = "configuration"
  type        = any
}

variable "custom_config" {
  type = any
}
```

**Good:**
```hcl
variable "instance" {
  description = "sample_resource"
  type = object({
    kind    = string
    flavor  = string
    version = string
    spec = object({
      operator_version = string
      high_availability = optional(object({
        replicas   = optional(number, 1)
      }))
      resources = optional(object({
        cpu_limit      = optional(string)
        memory_limit   = optional(string)
        cpu_request    = optional(string)
        memory_request = optional(string)
      }))
    })
  })
}

variable "custom_config" {
  type = object({
    setting_name = string
    setting_value = optional(string)
  })
}
```

---

## Quick Reference

| Rule | Category | Summary |
|------|----------|---------|
| RULE-001 | sample.spec | Required fields must be present |
| RULE-002 | sample.spec | Enum values must match schema |
| RULE-003 | sample.spec | Use {} for objects, [] for arrays |
| RULE-004 | var.inputs | Explicit object type required |
| RULE-005 | var.inputs | All facets.yaml inputs must exist |
| RULE-006 | var.inputs | var.inputs structure and access — default vs. attributes output types |
| RULE-007 | spec schema | No regex lookahead/lookbehind |
| RULE-008 | spec schema | No duplicate enum values |
| RULE-009 | spec schema | required inside patternProperties |
| RULE-010 | output schema | Explicit type: object for nested |
| RULE-011 | output schema | No union types |
| RULE-012 | output schema | Field names match actual outputs |
| RULE-013 | terraform | No required_providers in modules |
| RULE-014 | terraform | All variables must be declared; no platform-injected vars |
| RULE-015 | terraform | Use lookup() for optional spec fields |
| RULE-017 | terraform | depends_on: when to use and when not to |
| RULE-018 | terraform | Pin Docker images to verified tags |
| RULE-019 | terraform | Single owner for shared resource tags |
| RULE-020 | facets.yaml | No unsupported metadata in facets.yaml |
| RULE-021 | facets.yaml | facets.yaml must include intentDetails |
| RULE-022 | module design | Enable security defaults (encryption, logging) |
| RULE-023 | module lifecycle | Bump version for breaking changes; update project types |
| RULE-024 | terraform | Use //name module for resource name length limits |
| RULE-025 | terraform | Complete schema for var.instance and all variables; no type = any |
